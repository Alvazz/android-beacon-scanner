apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.bridou_n.beaconscanner"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 16
        versionName "2.1.2"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            keyAlias 'BeaconScannerKey'
            keyPassword beacon_scanner_key_password
            storeFile file(beacon_scanner_store_file)
            storePassword beacon_scanner_store_password
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            resValue "string", "app_name", "Beacon Scanner"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        preRelease {
            minifyEnabled true
            applicationIdSuffix = ".preRelease"
            versionNameSuffix = "-preRelease"
            resValue "string", "app_name", "Beacon Scanner"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
            resValue "string", "app_name", "Beacon Scanner"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

ext {
    supportLibraryVersion = '27.0.2'
    firebaseVersion = '11.6.2'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Dagger 2
    implementation 'com.google.dagger:dagger:2.11'
    kapt 'com.google.dagger:dagger-compiler:2.11'

    // RxJava & RxAndroid
    implementation 'io.reactivex.rxjava2:rxjava:2.1.2'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    // Retrofit & RxJava call adapters & Gson
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'

    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // Realm adapters
    implementation 'io.realm:android-adapters:2.1.0'

    // ALT Beacon library
    implementation 'org.altbeacon:android-beacon-library:2.12.1'

    // Easy permissions
    implementation 'pub.devrel:easypermissions:0.4.3'

    // Tap target view, discovery feature
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'

    // Constraint Layout
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Material dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.4.5'

    // Firebase Analytics & Crash reporting
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    implementation "com.google.firebase:firebase-crash:${firebaseVersion}"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.7'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
